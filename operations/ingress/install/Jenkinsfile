#!groovy

import groovy.json.JsonOutput

library 'kentrikos-shared-library'
def ingValue = '''
rbac:
 create: true

controller:
 service:
   annotations:
     service.beta.kubernetes.io/aws-load-balancer-type: nlb
     service.beta.kubernetes.io/aws-load-balancer-internal: true
   enableHttp: true
   enableHttps: false
'''

pipeline {

    agent any

    parameters {
        choice(name: 'K8S_FLAVOR', choices: ["eks", "kops"],
                description: 'Choose type of Kubernetes cluster (required for kops)')
    }
    environment {
        OPERATION_DIR = "operations/$AWS_REGION/env-$K8S_FLAVOR"
    }
    stages {
        stage('Git clone') {
            steps {
                gitCloneConfigRepo()
            }
        }

        stage('Switch K8S context') {
            steps {
                kubectlSwitchContextOps()
            }
        }

        stage('Deploy K8s Ingress controller') {
            steps {
                dir("$OPERATION_DIR") {
                    withProxyEnv() {
                        writeFile file: 'ing-values.yaml', text: ingValue
                        sh 'helm install --replace --wait opsing --namespace kube-system stable/nginx-ingress --set rbac.create=true --values ing-values.yaml'
                    }
                }
            }
        }

        stage('Create wildcard DNS record') {
            steps {
                dir("$OPERATION_DIR") {
                    withProxyEnv() {
                        script {
                            def parameters = readYaml file: 'jenkins/parameters.yaml'

                            println "Getting domain name"
                            def r53DomainName = sh(script: "aws route53 get-hosted-zone --id " + parameters.domainHostedZoneID + " --output text --query 'HostedZone.Name'",
                                                   returnStdout: true).trim().replaceAll("\\.\$", "")
                            r53WildcardRecordName = "*." + parameters.domainAliasPrefix + "." + r53DomainName

                            println "Getting ingress NLB information"
                            ingressNLBAddress = sh(script: "kubectl get service -n kube-system opsing-nginx-ingress-controller -o json | jq -r '.status.loadBalancer.ingress[0].hostname'",
                                    returnStdout: true).trim()
                            ingressNLBHostedZoneID = sh(script: "aws --region \$AWS_REGION elbv2 describe-load-balancers --query 'LoadBalancers[?DNSName==`" + ingressNLBAddress + "`].CanonicalHostedZoneId' --output text",
                                    returnStdout: true).trim()

                            println "Generating record for DNS update"
                            def r53WildcardRecordJSON = """
                            {
                                "Comment": "Creating Alias resource record sets in Route 53",
                                "Changes": [{
                                    "Action": "UPSERT",
                                    "ResourceRecordSet": {
                                        "Name": "${r53WildcardRecordName}",
                                        "Type": "A",
                                        "AliasTarget": {
                                            "HostedZoneId": "${ingressNLBHostedZoneID}",
                                            "DNSName": "${ingressNLBAddress}",
                                            "EvaluateTargetHealth": false
                                        }
                                    }
                                }]
                            }
                            """
                            writeFile file: 'opsing_r53_alias.json', text: r53WildcardRecordJSON
                            sh "cat opsing_r53_alias.json"

                            println "Creating/updating Route53 entry"
                            sh "aws route53 change-resource-record-sets --hosted-zone-id " + parameters.domainHostedZoneID + " --change-batch file://opsing_r53_alias.json"
                        }
                    }
                }
            }
        }
    }
}
