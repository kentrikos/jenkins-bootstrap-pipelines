#!groovy

pipeline {

    agent any

    parameters {
        choice(name: 'K8S_FLAVOR', choices: ["eks", "kops"],
               description: 'Choose type of Kubernetes cluster (required for kops)')
    }

    stages {
        stage('Git local config') {
            steps {
                sh 'git config --global --add user.name "example"'
                sh 'git config --global --add user.email "example@example.com"'
            }
        }
        stage('Git clone') {
            steps {
                cleanWs()
                git credentialsId: 'bitbucket-key', url: '$CONFIG_REPO_URL'
            }
        }

        stage('Get K8s cluster flavor') {
            steps {
                script {
                    if (env.K8S_FLAVOR == "eks") {
                      K8S_CLUSTER_NAME = "arn:aws:eks:$AWS_REGION:$AWS_OPERATIONS_ACCOUNT_NUMBER:cluster/$AWS_REGION-$PRODUCT_DOMAIN_NAME-$ENVIRONMENT_TYPE-ops"
                    } else {
                      K8S_CLUSTER_NAME = "$AWS_REGION-$PRODUCT_DOMAIN_NAME-$ENVIRONMENT_TYPE-ops.k8s.local"
                    }
                    println K8S_CLUSTER_NAME
                }
            }
        }

        stage('Switch kubectl context to ops') {
            steps {
                dir("operations/$AWS_REGION/env-$K8S_FLAVOR") {
                    script {
                      sh "kubectl config use-context $K8S_CLUSTER_NAME"
                    }
                }
            }
        }

       stage('Read DNS domain') {
            steps {
                dir("operations/$AWS_REGION/env-$K8S_FLAVOR") {
                    withProxyEnv() {
                        script {
                            def jenkinsParameters = readYaml file: 'jenkins/parameters.yaml'

                            println "Getting domain name"
                            def r53DomainName = sh(script: "aws route53 get-hosted-zone --id " + jenkinsParameters.domainHostedZoneID + " --output text --query 'HostedZone.Name'", 
                                                   returnStdout: true).trim().replaceAll("\\.\$", "")
                            prometheusEndpoint = "prometheus." + jenkinsParameters.domainAliasPrefix + "." + r53DomainName
                            grafanaEndpoint = "grafana." + jenkinsParameters.domainAliasPrefix + "." + r53DomainName
                        }
                    }
                }
            }
        }

        stage('Deploy Prometheus/ops') {
            steps {
                dir("operations/$AWS_REGION/env-$K8S_FLAVOR") {
                    withProxyEnv() {
                        script {
                            sh """
                            #!/bin/bash
                            helm install --replace --wait --name prometheus-ops --namespace lma stable/prometheus \
                            --set=server.ingress.enabled=true,server.ingress.hosts={$prometheusEndpoint}
                            """
                        }
                    }
                }
            }
        }
 
        stage('Add Prometheus/ops data-source to Grafana') {
            steps {
                dir("operations/$AWS_REGION/env-$K8S_FLAVOR") {
                    withProxyEnv() {
                        script {
                            def grafanaParameters = readYaml file: 'grafana/parameters.yaml'
                            def grafanaAdminPassword = grafanaParameters.defaultAdminPassword
                            def requestJson = """
                            {
                              "name": "Prometheus_ops",
                              "isDefault": false,
                              "type": "prometheus",
                              "url": "http://${prometheusEndpoint}",
                              "access": "proxy",
                              "basicAuth": true,
                              "basicAuthUser": "admin",
                              "basicAuthPassword": "admin"
                            }
                            """
                            sh "curl --verbose --user admin:${grafanaAdminPassword} http://${grafanaEndpoint}/api/datasources -X POST -H 'Content-Type: application/json;charset=UTF-8' --data-binary '${requestJson}'"
                        }
                    }
                }
            }
        }

        stage('Add Prometheus/ops dashboard to Grafana') {
            steps {
                dir("operations/$AWS_REGION/env-$K8S_FLAVOR") {
                    withProxyEnv() {
                        script {
                            def grafanaParameters = readYaml file: 'grafana/parameters.yaml'
                            def grafanaAdminPassword = grafanaParameters.defaultAdminPassword
                            println "Downloading and adjusting dashboard:"
                            sh "curl https://grafana.com/api/dashboards/6417/revisions/1/download -o grafana_dashboard_kubernetes_cluster.json"
                            dashboardJSON =  sh(script: 'jq --arg id 0 --arg uid kcops --arg title "Kubernetes Cluster/ops" ".id=\\\$uid | .uid=\\\$uid | .title=\\\$title" grafana_dashboard_kubernetes_cluster.json | sed -e "s/\\\${DS_PROMETHEUS}/Prometheus_ops/g"',
                                                returnStdout: true).trim().replaceAll("\\.\$", "")
                            println "Creating dashboard:"
                            def requestJson = """
                            {
                                "folderId": 0,
                                "overwrite": false,
                                "dashboard": $dashboardJSON
                            }
                            """
                            sh "curl --verbose --user admin:${grafanaAdminPassword} http://${grafanaEndpoint}/api/dashboards/db -X POST -H 'Content-Type: application/json;charset=UTF-8' --data-binary '${requestJson}'"
                        }
                    }
                }
            }
        }
    }
}

void withProxyEnv(List envVars = [], def body) {
    List proxies = []
    List envVaraibles = ['GIT_SSH_COMMAND=ssh -o StrictHostKeyChecking=no']
    withAWSParameterStore(credentialsId: '', namePrefixes: '', naming: 'relative', path: "/$PRODUCT_DOMAIN_NAME/$ENVIRONMENT_TYPE", recursive: true, regionName: "$AWS_REGION") {
        script {
            if (env.PROXY_HTTP) {
                proxies << "http_proxy=$PROXY_HTTP"
            }
            if (env.PROXY_HTTPS) {
                proxies << "https_proxy=$PROXY_HTTPS"
            }
            if (env.PROXY_NO) {
                proxies << "no_proxy=$PROXY_NO"
            }
            envVaraibles.addAll(proxies)
        }
    }
    envVaraibles.addAll(envVars)
    withEnv(envVaraibles) {
        body.call()
    }
}
