#!groovy


def prometheusValues = '''
server:
  service:
    type: LoadBalancer
    annotations:
      service.beta.kubernetes.io/aws-load-balancer-internal: 'true'
'''


pipeline {

    agent any

    parameters {
        choice(name: 'K8S_FLAVOR', choices: ["eks", "kops"],
               description: 'Choose type of Kubernetes cluster (required for kops)')
    }

    stages {
        stage('Git local config') {
            steps {
                sh 'git config --global --add user.name "example"'
                sh 'git config --global --add user.email "example@example.com"'
            }
        }
        stage('Git clone') {
            steps {
                cleanWs()
                git credentialsId: 'bitbucket-key', url: '$CONFIG_REPO_URL'
            }
        }

        stage('Get K8s cluster flavor') {
            steps {
                script {
                    if (env.K8S_FLAVOR == "eks") {
                      K8S_CLUSTER_NAME = "arn:aws:eks:$AWS_REGION:$AWS_APPLICATION_ACCOUNT_NUMBER:cluster/$AWS_REGION-$PRODUCT_DOMAIN_NAME-$ENVIRONMENT_TYPE"
                    } else {
                      K8S_CLUSTER_NAME = "$AWS_REGION-$PRODUCT_DOMAIN_NAME-$ENVIRONMENT_TYPE.k8s.local"
                    }
                    println K8S_CLUSTER_NAME
                }
            }
        }

        stage('Switch kubectl context to app') {
            steps {
                dir("application/$AWS_REGION/env-$K8S_FLAVOR") {
                    script {
                      sh "kubectl config use-context $K8S_CLUSTER_NAME"
                    }
                }
            }
        }

        stage('Check/create LMA namespace') {
            steps {
                dir("application/$AWS_REGION/env-$K8S_FLAVOR") {
                    withProxyEnv() {
                        withAWS(role: "$CROSS_ACCOUNT_ROLE_NAME", roleAccount: "$AWS_APPLICATION_ACCOUNT_NUMBER") {
                            script {
                                sh '''
                                #!/bin/bash -x
                                if ! kubectl get namespace lma;
                                then 
                                    echo "Namespace for LMA does not exist, creating..."
                                    kubectl create namespace lma
                                fi
                                '''
                            }
                        }
                    }
                }
            }
        }

       stage('Deploy Prometheus/app') {
            steps {
                dir("application/$AWS_REGION/env-$K8S_FLAVOR") {
                    withProxyEnv() {
                        writeFile file: 'prometheus-values.yaml', text: prometheusValues
                        withAWS(role: "$CROSS_ACCOUNT_ROLE_NAME", roleAccount: "$AWS_APPLICATION_ACCOUNT_NUMBER") {
                            script {
                                /*
                                 * FIXME: ingress should be used (to be implemented after deciding how to handle R53 domains in app account)
                                 */
                                sh "helm install --replace --wait --name prometheus-app --namespace lma stable/prometheus --values prometheus-values.yaml"
                            }
                        }
                    }
                }
            }
       }

       stage('Read DNS information') {
            steps {
                dir("operations/$AWS_REGION/env-$K8S_FLAVOR") {
                    withProxyEnv() {
                        script {
                            def jenkinsParameters = readYaml file: 'jenkins/parameters.yaml'

                            println "Getting domain name for Grafana"
                            def r53DomainName = sh(script: "aws route53 get-hosted-zone --id " + jenkinsParameters.jxDomainHostedZoneID + " --output text --query 'HostedZone.Name'",
                                                   returnStdout: true).trim().replaceAll("\\.\$", "")
                            grafanaEndpoint = "grafana." + jenkinsParameters.jxDomainAliasPrefix + "." + r53DomainName
                        }
                        withAWS(role: "$CROSS_ACCOUNT_ROLE_NAME", roleAccount: "$AWS_APPLICATION_ACCOUNT_NUMBER") {
                            script {
                                println "Getting ELB DNS name for Prometheus"
                                prometheusEndpoint = sh(script: "kubectl -n lma get service prometheus-app-server -o json | jq -r '.status.loadBalancer.ingress[].hostname'",
                                                        returnStdout: true).trim().replaceAll("\\.\$", "")
                            }
                        }
                    }
                }
            }
        }

        stage('Add Prometheus/app data-source to Grafana') {
            steps {
                dir("operations/$AWS_REGION/env-$K8S_FLAVOR") {
                    withProxyEnv() {
                        script {
                            def grafanaParameters = readYaml file: 'grafana/parameters.yaml'
                            def grafanaAdminPassword = grafanaParameters.defaultAdminPassword
                            def requestJson = """
                            {
                              "name": "Prometheus_app",
                              "isDefault": false,
                              "type": "prometheus",
                              "url": "http://${prometheusEndpoint}",
                              "access": "proxy",
                              "basicAuth": true,
                              "basicAuthUser": "admin",
                              "basicAuthPassword": "admin"
                            }
                            """
                            sh "curl --verbose --user admin:${grafanaAdminPassword} http://${grafanaEndpoint}/api/datasources -X POST -H 'Content-Type: application/json;charset=UTF-8' --data-binary '${requestJson}'"
                        }
                    }
                }
            }
        }

        stage('Add Prometheus/app dashboard do Grafana') {
            steps {
                dir("operations/$AWS_REGION/env-$K8S_FLAVOR") {
                    withProxyEnv() {
                        script {
                            def grafanaParameters = readYaml file: 'grafana/parameters.yaml'
                            def grafanaAdminPassword = grafanaParameters.defaultAdminPassword
                            println "Downloading and adjusting dashboard:"
                            sh "curl https://grafana.com/api/dashboards/6417/revisions/1/download -o grafana_dashboard_kubernetes_cluster.json"
                            dashboardJSON =  sh(script: 'jq --arg id 0 --arg uid kcapp --arg title "Kubernetes Cluster/app" ".id=\\\$uid | .uid=\\\$uid | .title=\\\$title" grafana_dashboard_kubernetes_cluster.json | sed -e "s/\\\${DS_PROMETHEUS}/Prometheus_app/g"',
                                                returnStdout: true).trim().replaceAll("\\.\$", "")
                            println "Creating dashboard:"
                            def requestJson = """
                            {
                                "folderId": 0,
                                "overwrite": false,
                                "dashboard": $dashboardJSON
                            }
                            """
                            sh "curl --verbose --user admin:${grafanaAdminPassword} http://${grafanaEndpoint}/api/dashboards/db -X POST -H 'Content-Type: application/json;charset=UTF-8' --data-binary '${requestJson}'"
                        }
                    }
                }
            }
        }
    }
}


void withProxyEnv(List envVars = [], def body) {
    List proxies = []
    List envVaraibles = ['GIT_SSH_COMMAND=ssh -o StrictHostKeyChecking=no']
    withAWSParameterStore(credentialsId: '', namePrefixes: '', naming: 'relative', path: "/$PRODUCT_DOMAIN_NAME/$ENVIRONMENT_TYPE", recursive: true, regionName: "$AWS_REGION") {
        script {
            if (env.PROXY_HTTP) {
                proxies << "http_proxy=$PROXY_HTTP"
            }
            if (env.PROXY_HTTPS) {
                proxies << "https_proxy=$PROXY_HTTPS"
            }
            if (env.PROXY_NO) {
                proxies << "no_proxy=$PROXY_NO"
            }
            envVaraibles.addAll(proxies)
        }
    }
    envVaraibles.addAll(envVars)
    withEnv(envVaraibles) {
        body.call()
    }
}
